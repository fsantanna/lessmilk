#include "sdl.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;

input _SDL_KeyboardEvent*    SDL_KEYDOWN;

var int win_w;
var int win_h;
var _SDL_Window* win;
    finalize
        win = _SDL_CreateWindow("Lessmilk 1", 500, 1300, 800, 480, _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(win);
    end

_SDL_GetWindowSize(win, &win_w, &win_h);

var _SDL_Renderer[] ren;
    finalize
        ren = _SDL_CreateRenderer(win, -1, 0);
    with
        _SDL_DestroyRenderer(ren);
    end
    
#include "player.ceu"
#include "enemy.ceu"
par/or do
    await SDL_QUIT;
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(ren, 0,0,0,0x00);
        _SDL_RenderFillRect(ren, null);
    end
with 
    var Player player with
        this.ren = ren;
    end;
    await FOREVER;
with
    pool Enemy[10] enemies;
    every 500ms do
        spawn Enemy in enemies with
           this.ren = ren;
           if _rand()%2 then 
               this.x0 = 0; 
               this.xf = win_w;
               this.y0 = _rand()%win_h; 
               this.yf = _rand()%win_h; 
           else
               this.y0 = 0; 
               this.yf = win_h;
               this.x0 = _rand()%win_w; 
               this.xf = _rand()%win_w;
           end
          this.speed = 100;
        end;
    end
with
    every SDL_REDRAW do
        _SDL_RenderPresent(ren);
    end
end

escape 0;
