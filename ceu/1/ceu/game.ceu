input int SDL_DT;
input _SDL_KeyboardEvent*    SDL_KEYDOWN;

#include "player.ceu"
#include "enemy.ceu"

class Game with
    event void ok;
do
    var int score = 0;
    var int level = 0;
    var char[5] score_txt;
    var int  score_size = 0;
            
    var Player player;
    pool Enemy[20] enemies;
    par do
        every 500ms do
            spawn Enemy in enemies;
        end
    with
        var int ms;
        every ms in SDL_DT do
            loop (Enemy *)e in enemies do
               //if SDLRect_vs_SDLRect(e:col_box,player.col_box)then
                if not (((e:col_box.y+e:col_box.h) < player.col_box.y) or ((player.col_box.y+player.col_box.h) < e:col_box.y) or ((e:col_box.x+e:col_box.w) < player.col_box.x) or ((player.col_box.x+player.col_box.w) < e:col_box.x)) then
                  _printf("~~#~~ SCORE  %d ~~#~~\n",score);
                  emit ok;
                end
            end        
        end
    with
        every 1s do
            score = score + 1;
        end
    with
        every SDL_REDRAW do
            native @nohold _sprintf();
            score_size = _sprintf(score_txt,"%d",score); 
            var _SDL_Texture* str_tex;
            var _SDL_Rect str_r;
            var _SDL_Color clr;
                clr.r = 255;
                clr.g = 255;
                clr.b = 255;
            
            str_r.x = 10;
            str_r.y = 10;
            str_r.w = 10*score_size;
            str_r.h = 20;

/// TODO: cria e destroy o texto a cada frame!
/// opcao: criar todos os algarismos em um vetor [0..9] em "load.ceu"
///        e aqui fazer apenas o "RenderCopy" com o valor corrente
            var _SDL_Surface* sfc;
            finalize
                sfc = _TTF_RenderText_Blended(_fnt, score_txt, clr);
            with
                _SDL_FreeSurface(sfc);
            end
            finalize
                str_tex = _SDL_CreateTextureFromSurface(_REN, sfc);
            with
                _SDL_DestroyTexture(str_tex);
            end
            _SDL_RenderCopy(_REN, str_tex, null, &str_r);
        end
    end
end
	
