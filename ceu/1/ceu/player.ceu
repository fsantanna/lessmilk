class Player with
    var _SDL_Rect col_box; 
do
    var int tex_w,tex_h;
    _SDL_QueryTexture(_texPLAYER,null,null,&tex_w,&tex_h);

    var _SDL_Rect clip;
    clip.x = 0;
    clip.y = 0;
    clip.w = tex_w/2;
    clip.h = tex_h/4;
    
    this.col_box.x = WIN_W/2;
    this.col_box.y = WIN_H/2;
    this.col_box.w = clip.w;
    this.col_box.h = clip.h;
    
    var _SDL_KeyboardEvent* key;
    var bool walk_st;
    par/and do
         every SDL_REDRAW do
            _SDL_RenderCopy(_REN,_texPLAYER, &clip,&this.col_box);
        end
    with
        var float x,y,speed_x,speed_y;
        x = WIN_W/2;
        y = WIN_H/2;
        var int dt;
        every dt in SDL_DT do
            speed_x = 0;
            speed_y = 0;
            walk_st = not walk_st;
            var u8* keyStates;
            finalize
                keyStates = _SDL_GetKeyboardState( null );
            with
                nothing;
            end
            if keyStates[_SDL_SCANCODE_DOWN] then
                speed_x = 0;
                speed_y = 300;
                clip.y = 0*clip.h;
            else/if keyStates[_SDL_SCANCODE_UP] then
                speed_x = 0;
                speed_y = -300;
                clip.y = 2*clip.h;
            else/if keyStates[_SDL_SCANCODE_RIGHT] then
                speed_x = 300;
                speed_y = 0;
                clip.y = 1*clip.h;
            else/if keyStates[_SDL_SCANCODE_LEFT] then
                speed_x = -300;
                speed_y = 0;
                clip.y = 3*clip.h;
            else
                walk_st = not walk_st;
            end
            if walk_st then
                clip.x = tex_w/2;
            else
                clip.x = 0;
            end
            x = x + dt*speed_x/1000;
            y = y + dt*speed_y/1000;
            if not(x > WIN_W or x < 0 or y > WIN_H or y < 0) then
                this.col_box.x = x;
                this.col_box.y = y;
            end

        end
    end
end


