#include "bullet.ceu"
class Player with
    var _SDL_Rect col_box; 
do
    var int tex_w,tex_h;
    _SDL_QueryTexture(_texPLAYER,null,null,&tex_w,&tex_h);

    var _SDL_Rect clip;
    clip.x = 0;
    clip.y = 0;
    clip.w = tex_w;
    clip.h = tex_h;
    
    this.col_box.x = WIN_W/2;
    this.col_box.y = WIN_H-clip.h;
    this.col_box.w = clip.w;
    this.col_box.h = clip.h;
    
    var _SDL_KeyboardEvent* key;

    var bool weapon_cooldown;
    par/and do
         every SDL_REDRAW do
            _SDL_RenderCopy(_REN,_texPLAYER, &clip,&this.col_box);
        end
    with
        every 500ms do
            weapon_cooldown = true;
        end
    with
        var float x,y,speed_x,speed_y;
        pool Bullet[5] bullets;
        x = col_box.x;
        y = col_box.y;
        var int dt;
        every dt in SDL_DT do
            speed_x = 0;
            speed_y = 0;
            var u8* keyStates;
            finalize
                keyStates = _SDL_GetKeyboardState( null );
            with
                nothing;
            end
            if keyStates[_SDL_SCANCODE_UP] and weapon_cooldown then
                speed_x = 0;
                spawn Bullet in bullets with
                    this.col_box.x = col_box.x+col_box.w/2; 
                    this.col_box.y = col_box.y; 
                end;
                weapon_cooldown = false;
            else/if keyStates[_SDL_SCANCODE_RIGHT] then
                speed_x = 300;
                speed_y = 0;
            else/if keyStates[_SDL_SCANCODE_LEFT] then
                speed_x = -300;
                speed_y = 0;
            end
            x = x + speed_x/100;
            y = y + speed_y/100;
            if not(x > WIN_W or x < 0 or y > WIN_H or y < 0) then
                this.col_box.x = x;
                this.col_box.y = y;
            end

        end
    end
end


